import com.liferay.gradle.plugins.gulp.ExecuteGulpTask
import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction

import groovy.json.JsonBuilder

buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins", version: "1.0.56+"
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.gulp", version: "1.0.0+"
	}

	repositories {
		mavenLocal()

		maven {
			url "http://cdn.repository.liferay.com/nexus/content/groups/public"
		}
	}
}

task distBundle(type: Zip)
task initBundle(type: Copy)

configurations {
	bundle
}

dependencies {
	bundle group: "com.liferay", name: "portal-tomcat-bundle", version: "7.0-ce-b1-20151223111037302"
}

distBundle {
	archiveName = "bundle.zip"
	destinationDir = buildDir

	from {
		zipTree(configurations.bundle.singleFile)
	} {
		eachFile new StripPathSegmentsAction(1)
	}

	includeEmptyDirs = false
}

initBundle {
	doFirst {
		delete destinationDir
	}

	from {
		zipTree(configurations.bundle.singleFile)
	} {
		eachFile new StripPathSegmentsAction(1)
	}

	from ("${rootDir}/configs/common")
	from ("${rootDir}/configs/${project.properties["liferay.environment"]}")

	includeEmptyDirs = false

	into gradle.appServerParentDir
}

repositories {
	maven {
		url "https://liferay-test-01.ci.cloudbees.com/job/liferay-bundle-publishing/lastSuccessfulBuild/artifact/build/m2_repo/"
	}
}

configure(subprojects.findAll {!it.subprojects && FileUtil.isChild(it.projectDir, gradle.modulesDir)}) {
	repositories {
		maven {
			url "http://cdn.repository.liferay.com/nexus/content/groups/public"
		}
	}

	apply plugin: "com.liferay.plugin"

	distBundle {
		into("deploy") {
			from jar
		}
	}

	liferay {
		appServerParentDir = gradle.appServerParentDir
	}
}

configure(subprojects.findAll {!it.subprojects && FileUtil.isChild(it.projectDir, gradle.themesDir)}) {
	apply plugin: "base"
	apply plugin: "com.liferay.gulp"

	task createLiferayThemeJson
	task deploy(dependsOn: "gulpDeploy")

	buildDir = file("build_gradle")

	assemble {
		dependsOn "gulpBuild"
	}

	clean {
		delete "build", "dist"
		dependsOn cleanNpmInstall
	}

	createLiferayThemeJson {
		File liferayThemeJsonFile = file("liferay-theme.json")

		doLast {
			JsonBuilder jsonBuilder = new JsonBuilder()

			File appServerDir = new File(gradle.appServerParentDir, "tomcat-7.0.62")
			File appServerThemeDir = new File(appServerDir, "webapps/" + project.name)
			File deployDir = new File(gradle.appServerParentDir, "deploy")

			jsonBuilder.LiferayTheme {
				appServerPath appServerDir.absolutePath
				appServerPathTheme appServerThemeDir.absolutePath
				deployed false
				deployPath deployDir.absolutePath
				themeName project.name
			}

			liferayThemeJsonFile.text = jsonBuilder
		}

		onlyIf {
			!liferayThemeJsonFile.exists()
		}
	}

	distBundle {
		into("deploy") {
			from fileTree("dist") {
				builtBy "gulpBuild"
				include "*.war"
			}
		}
	}

	tasks.withType(ExecuteGulpTask) {
		dependsOn createLiferayThemeJson, npmInstall

		args "--"
	}
}

Project pluginsSDKProject = findProject(":plugins-sdk")

if (pluginsSDKProject) {
	configure(pluginsSDKProject) {
		ant.importBuild "build.xml"

		task build
		task updateSDKProperties

		build {
			dependsOn rootProject.initBundle
			dependsOn war

			war.mustRunAfter(rootProject.initBundle)
		}

		distBundle {
			into("deploy") {
				from fileTree("dist") {
					builtBy "war"
					include "*.war"
				}
			}
		}

		updateSDKProperties << {
			String hostName = InetAddress.localHost.hostName

			File buildPropertiesFile = file("build.${hostName}.properties")

			Properties buildProperties = FileUtil.readProperties(buildPropertiesFile)

			buildProperties["app.server.parent.dir"] = FileUtil.getAbsolutePath(gradle.appServerParentDir)

			buildPropertiesFile.withWriter {
				buildProperties.store it, null
			}
		}
	}
}

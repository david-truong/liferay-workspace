import com.liferay.gradle.plugins.gulp.ExecuteGulpTask
import com.liferay.gradle.util.FileUtil
import com.liferay.gradle.util.copy.StripPathSegmentsAction

import groovy.json.JsonBuilder

buildscript {
	dependencies {
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins", version: "1.0.56+"
		classpath group: "com.liferay", name: "com.liferay.gradle.plugins.gulp", version: "1.0.0+"
	}

	repositories {
		mavenLocal()

		maven {
			url "http://cdn.repository.liferay.com/nexus/content/groups/public"
		}
	}
}

task distBundle(type: Zip)
task initBundle(type: Copy)

configurations {
	bundle
}

dependencies {
	bundle group: gradle.bundleGroup, name: gradle.bundleName, version: gradle.bundleVersion
}

distBundle {
	archiveName = "bundle.zip"
	destinationDir = buildDir

	from {
		zipTree(configurations.bundle.singleFile)
	} {
		eachFile new StripPathSegmentsAction(1)
	}

	includeEmptyDirs = false
}

initBundle {
	doFirst {
		delete destinationDir
	}

	from {
		zipTree(configurations.bundle.singleFile)
	} {
		eachFile new StripPathSegmentsAction(1)
	}

	from ("${rootDir}/configs/common")
	from ("${rootDir}/configs/${gradle.environment}")

	includeEmptyDirs = false

	into gradle.homeDir
}

repositories {
	maven {
		url gradle.bundleMavenUrl
	}
}

configure(subprojects.findAll {!it.subprojects && FileUtil.isChild(it.projectDir, gradle.modulesDir)}) {
	repositories {
		maven {
			url "http://cdn.repository.liferay.com/nexus/content/groups/public"
		}
	}

	apply plugin: "com.liferay.plugin"

	buildLang {
		translateClientId = "microsoft.translator.client.id"
		translateClientSecret = "microsoft.translator.client.secret"
	}

	distBundle {
		into("deploy") {
			from jar
		}
	}

	liferay {
		appServerParentDir = gradle.homeDir
	}
}

configure(subprojects.findAll {!it.subprojects && FileUtil.isChild(it.projectDir, gradle.themesDir)}) {
	apply plugin: "base"
	apply plugin: "com.liferay.gulp"

	task createLiferayThemeJson
	task deploy(dependsOn: "gulpDeploy")

	buildDir = file("build_gradle")

	assemble {
		dependsOn "gulpBuild"
	}

	clean {
		delete "build", "dist"
		dependsOn cleanNpmInstall
	}

	createLiferayThemeJson {
		File liferayThemeJsonFile = file("liferay-theme.json")

		doLast {
			JsonBuilder jsonBuilder = new JsonBuilder()

			File appServerDir = new File(gradle.homeDir, "tomcat-7.0.62")
			File appServerThemeDir = new File(appServerDir, "webapps/" + project.name)
			File deployDir = new File(gradle.homeDir, "deploy")

			jsonBuilder.LiferayTheme {
				appServerPath appServerDir.absolutePath
				appServerPathTheme appServerThemeDir.absolutePath
				deployed false
				deployPath deployDir.absolutePath
				themeName project.name
			}

			liferayThemeJsonFile.text = jsonBuilder
		}

		onlyIf {
			!liferayThemeJsonFile.exists()
		}
	}

	distBundle {
		into("deploy") {
			from fileTree("dist") {
				builtBy "gulpBuild"
				include "*.war"
			}
		}
	}

	tasks.withType(ExecuteGulpTask) {
		dependsOn createLiferayThemeJson, npmInstall
	}
}

Project pluginsSDKProject = findProject(":plugins-sdk")

if (pluginsSDKProject) {
	configure(pluginsSDKProject) {
		ant.importBuild "build.xml"

		task build
		task updateSDKProperties

		build {
			dependsOn war
		}

		distBundle {
			into("deploy") {
				from fileTree("dist") {
					builtBy "war"
					include "*.war"
				}
			}
		}

		updateSDKProperties << {
			String hostName = InetAddress.localHost.hostName

			File buildPropertiesFile = file("build.${hostName}.properties")

			Properties buildProperties = FileUtil.readProperties(buildPropertiesFile)

			buildProperties["app.server.parent.dir"] = FileUtil.getAbsolutePath(gradle.homeDir)

			buildPropertiesFile.withWriter {
				buildProperties.store it, null
			}
		}

		war {
			if (!gradle.homeDir.exists()) {
				dependsOn rootProject.initBundle
			}
		}
	}
}

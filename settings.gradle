gradle.ext.homeDir = file(properties["liferay.workspace.home.dir"])

gradle.ext.modulesDir = file(properties["liferay.workspace.modules.dir"])
gradle.ext.themesDir = file(properties["liferay.workspace.themes.dir"])
gradle.ext.pluginsSDKDir = file(properties["liferay.workspace.plugins.sdk.dir"])

FileTree modulesFileTree = fileTree(dir: gradle.modulesDir, include: "**/bnd.bnd")
FileTree themesFileTree = fileTree(dir: gradle.themesDir, excludes: ["**/build/", "**/build_gradle/", "**/node_modules/"], include: "**/package.json")

FileTree fileTree = modulesFileTree + themesFileTree

URI rootURI = rootDir.toURI()

fileTree.each {
	File file ->

	String dirName = rootURI.relativize(file.parentFile.toURI())

	include ":" + dirName.replaceAll("[\\/]", ":")
}

if (gradle.pluginsSDKDir.exists()) {
	include ":plugins-sdk"

	ProjectDescriptor pluginsSDKProject = project(":plugins-sdk")

	pluginsSDKProject.projectDir = gradle.pluginsSDKDir
}